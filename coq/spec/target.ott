%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% METAVARIABLES

% metavar var, x , y ::= {{ repr-locally-nameless }}

% metavar lit, b ::= {{ coq nat }}

% metavar label, i, l ::= {{ coq string }}

%% metavar tlabel, ll ::= {{ coq string }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% GRAMMAR (LNgen portion)

grammar

ttyp, At, Bt, Ct :: 'ttyp_' ::= {{ com types }}
  | Top              ::  :: top    {{ com top type }} {{ tex \top }}
  | Bot              ::  :: bot    {{ com bottom type }} {{ tex \bot }}
  | Base             ::  :: base   {{ com base type }} {{ tex \mathbb{B} }}
  | At -> Bt         ::  :: arrow   {{ com function types }}
  | l : At , Bt     ::  :: rcd     {{ com record }} {{ tex \{ [[ll]] [[:]] [[At]] [[,]] [[Bt]]  \} }}
  | { l : At }      :: M:: single_rcd {{ coq (ttyp_rcd [[ll]] [[At]] ttyp_top) }}
  % | At & Bt          ::  :: and     {{ com intersection }}
  | ( At )           :: S:: paren   {{ coq [[At]] }}
  | || A ||          :: M:: stype_trans {{ tex ||[[A]]|| }}  {{ coq (styp2ttyp ([[A]])) }}
  

texp, t, te, tp, tv, tu :: 'texp_' ::=              {{ com target term  }}
  | x                                 :: :: var     {{ com variable }}
  | b                                 :: :: base    {{ com base value }} {{ tex \mathsf{[[b]]}  }}
  | \ x . t                           :: :: abs     (+ bind x in t +)
                                                        {{ com abstractions }}
  | fix x . t                         :: :: fixpoint
                                                        (+ bind x in t +)
                                                        {{ com fixpoint }}
  | t1 t2                             :: :: app     {{ com applications }}
  | { }                               ::  :: nil    {{ com empty record }} {{ tex \{ \} }}
  | l => t1' , t2                     ::  :: cons   {{ tex \{ [[ll]] [[=>]] [[t1']] [[,]] [[t2]]  \} }}
  | { l => t1' }                      :: S:: single_rec {{ coq (texp_cons [[ll]] [[t1']] texp_nil) }}
  | | A | => t1' , t2                 :: M:: lcons   {{ coq (texp_cons (stype2string [[A]]) [[t1']] [[t2]]) }}
  | { | A | => t1' }                  :: M:: lsingle_rec {{ coq (texp_cons (stype2string [[A]]) [[t1']] texp_nil) }}
  | t1 . l                            ::  :: proj     {{ com projection }}
  | t1 . | A |                        :: M:: lproj    {{ coq (texp_proj [[t1]] (stype2string [[A]])) }}
  | t1 ; t2                           ::  :: concat   {{ com concatenation }}
  | ( t )                             :: S:: paren    {{ coq [[t]] }}
  | t1 [ x ~>> t2 ]                   :: M:: tsubst   {{ coq (open_texp_wrt_texp [[x t1]][[t2]]) }}

substitutions
  single texp x  :: subst

freevars
  texp x     :: fv


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% GRAMMAR  (non LNgen portion)

grammar

tctx, Gt {{ tex \Gamma }} :: 'target_ctx_' ::= {{ com typing context }} {{ coq list ( atom * ttyp ) }}
  | []           ::  :: empty      {{ coq nil }}
                                   {{ tex \cdot }}
                                   {{ com empty context }}
  | Gt , x : At    ::  :: cons     {{ coq (cons ([[x]],[[At]]) [[Gt]]) }}
                                   {{ com assumption }}

% tvl :: 'tvl_' ::=  {{ com target value or projection label }}
%   | tv            ::  :: exp
%   | { l }        ::  :: la


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Reduction of type-indexed merge
embed
{{ coq
Definition string_eq_dec : forall (x y : string), { x = y } + { x <> y }.
Proof.
  repeat decide equality.
Defined.

Fixpoint tlookup (i:string) (tr:texp) : option texp :=
  match tr with
  | texp_cons ti t tr' => if string_eq_dec i ti then Some t else tlookup i tr'
  | _ => None
  end.

Fixpoint Tlookup (i:string) (T:ttyp) : option ttyp :=
  match T with
  | ttyp_rcd ti At Bt => if string_eq_dec i ti then Some At else Tlookup i Bt
  | _ => None
  end.
  
Inductive subTarget : ttyp -> ttyp -> Prop :=
 | ST_refl : forall At,
     subTarget At At
| ST_arrow: forall At Bt At' Bt',
    subTarget At At' -> subTarget At' At -> subTarget Bt Bt' -> subTarget Bt' Bt -> subTarget (ttyp_arrow At Bt) (ttyp_arrow At' Bt')
 | ST_rcd : forall At Bt,
     [[recTyp At]] -> [[recTyp Bt]] ->
     (forall l Ct, Tlookup l At = Some Ct -> exists Ct', Tlookup l Bt = Some Ct' /\ subTarget Ct' Ct /\ subTarget Ct Ct') ->
     subTarget Bt At.

#[export] Hint Constructors subTarget : core.
}}

defns
Values :: '' ::=

  defn
  value tv ::  :: value :: value_ {{ com Values }}
  by

  ---------------------- :: unit
  value { }

  ---------------------- :: lit
  value b

  ---------------------- :: abs
  value \x . t

  value tv1
  value tv2
  ----------------------- :: merge
  value l => tv1 , tv2


defns
TargetStep   :: '' ::=

  defn
  t >-> t'
  :: :: target_step :: 'TS_'
  {{ com Record Calculus Structrual Reduction }}
  by

  t >-> t'
  -------------------- :: Proj1
  t . l >-> t' . l
  
  t >-> t'
  -------------------- :: AppL
  t t2 >-> t' t2

  value tv
  t >-> t'
  -------------------- :: AppR
  tv t >-> tv t'

  t >-> t'
  -------------------- :: MergeL
  t ; t2 >-> t' ; t2

  value tv
  t >-> t'
  -------------------- :: MergeR
  tv ; t >-> tv ; t'

  t >-> t'
  --------------------------------- :: RcdHead
  l => t , t2 >-> l => t' , t2

  value tv
  t >-> t'
  --------------------------------- :: RcdTail
  l => tv , t >-> l => tv , t'

  value tv
  -------------------- :: MergeEmpty
  {} ; tv >-> tv

  value tv1
  value tv2
  value tv3
  --------------------------------------------------- :: MergeRcd
  (l => tv1 , tv2) ; tv3 >-> l => tv1 , (tv2 ; tv3)

  value tv
  lookup l tv => t
  --------------------------------------------------- :: ProjRcd
  tv . l >-> t
  
  ----------------------------- :: AppAbs
  (\ x . t)  tv  >-> t [x~>>tv]

  ----------------------------- :: Fixpoint
  fix x.t  >-> t [x~>>fix x.t]



defns
RecordTypes   :: '' ::=

  defn
  recTyp At
  :: :: rec_typ :: 'RT_'
  {{ com Valid record types }}
  by

  -------------------- :: Nil
  recTyp Top

  recTyp Bt
  -------------------- :: Rcd
  recTyp l : At , Bt


% defns
% TargetEqIndexType :: '' ::=

%   defn
%   At ~= Bt :: :: eqIndTypTarget :: TEI_
%   {{ com Types with the same index }}
%   by

%   --------------- :: top
%   Top ~= Top

%   --------------- :: bot
%   Bot ~= Bot

%   --------------- :: base
%   Base ~= Base
  
%   % At ~= Bt
%   % Bt ~= Ct
%   % --------------- :: trans
%   % At ~= Ct

%   % Bt ~= At
%   % --------------- :: symm
%   % At ~= Bt

%   At1 ~= At2
%   Bt1 ~= Bt2
%   ---------------------- :: arrow
%   At1->Bt1 ~= At2->Bt2

%   recTyp Ct
%   recTyp Ct'
%   (lookup l Ct => At' and At' ~= At) or lookup l Ct =/> 
%   (lookup l Ct' => Bt' and Bt' ~= Bt) or lookup l Ct' =/> 
%   At ~= Bt
%   Ct ~= Ct'
%   ------------------------- :: rcd
%   l:At, Ct ~= l:Bt, Ct'

%   recTyp Ct
%   (lookup l1 Ct => At' and At' ~= At) or lookup l1 Ct =/> 
%   (lookup l2 Ct => Bt' and Bt' ~= Bt) or lookup l2 Ct =/> 
%   l1 != l2
%   l2:Bt, (l1:At, Ct) ~= Ct'
%   ---------------------------------------------- :: comm
%   l1:At, (l2:Bt, Ct) ~= Ct'

%   % recTyp Ct
%   % (lookup l Ct => At' and At' ~= At) or lookup l Ct =/> 
%   % At ~= Bt
%   % l:Bt, (l:At, Ct) ~= Ct'
%   % ---------------------------------------------- :: dup
%   % l:At, (l:Bt, Ct) ~= Ct'

%   % --------------- :: comm
%   % At&Bt ~= Bt&At

%   % -------------------- :: assoc
%   % At&(Bt&Ct) ~= (At&Bt)&Ct

%   % toplike A
%   % --------------- :: top
%   % A&B ~= B

%   % A1 ~= A2
%   % -------------------- :: dup
%   % (A1&A2)&B ~= A1&B
  
defns
ConcatTypes   :: '' ::=

  defn
  At & Bt => Ct
  :: :: concat_typ :: 'CT_'
  {{ com Concatenate types }}
  by

  recTyp Bt
  -------------------- :: Nil
  Top & Bt => Bt


  (lookup l Bt2 => At' and At' ~= At) or lookup l Bt2 =/>
  Bt1 & Bt2 => Ct
  ---------------------------------------- :: Rcd
  (l : At , Bt1) & Bt2 => l : At , Ct

defns
ContainedByRecTyp   :: '' ::=

  defn
  At |> l : Bt
  :: :: contained_by_rec_typ :: 'CRT_'
  {{ com non-deterministic lookup }}
  by

  ------------------------------- :: Head
  (l1 : At , Bt) |> l1 : At

  Bt |> l2 : Ct
  ------------------------------- :: Tail
  (l1 : At , Bt) |> l2 : Ct




% defns
% TargetSubtype :: '' ::=

%   defn
%   At < Bt :: :: SubtypeTarget :: TS_
%   {{ com Types with the same index }}
%   by

%   At ~= Bt
%   --------------- :: refl
%   At < Bt

%   --------------- :: top
%   At < Top

%   Bt < Ct
%   --------------- :: wide
%   l:At, Bt < Ct


defns
WelformedTypes   :: '' ::=

  defn
  |- At
  :: :: wf_typ :: 'WF_'
  {{ com Type welformedness }}
  by

  -------------------- :: Nil
  |- Top

  -------------------- :: Bot
  |- Bot

  -------------------- :: Base
  |- Base

  |- At
  |- Bt
  recTyp Bt
  (lookup l Bt => At' and At' ~= At) or lookup l Bt =/>
  ----------------------------------------- :: Rcd
  |- l : At , Bt

  |- At
  |- Bt
  -------------------- :: Arrow
  |- At -> Bt

defns
WelformedCtx   :: '' ::=

  defn
  |- Gt
  :: :: wf_ctx :: 'WFC_'
  {{ com Context welformedness }}
  by

  -------------------- :: Nil
  |- []

  |- At
  |- Gt
  -------------------- :: Cons
  |- Gt , x : At


defns
TargetSubtype :: '' ::=

  defn
  At < Bt :: :: SubtypeTarget :: TS_
  {{ tex [[At]] [[<:]] [[Bt]] }}
  {{ com Target Subtyping }}
  by

  |- At
  --------------- :: top
  At < Top
  
  |- At
  --------------- :: refl
  At < At

  At1 < At2
  Bt1 < Bt2
  ---------------------- :: arrow
  At1->Bt1 < At2->Bt2

  recTyp Ct'
  lookup l Ct => Bt
  Bt < At
  % if we use Bt < At, we wil need At < At for general At
  (lookup l Ct' => At' and At' ~= At) or lookup l Ct' =/>
  Ct < Ct'
  --------------- :: rcd
  Ct < l:At, Ct'
  
defns
TargetTyping :: '' ::=

  defn
  Gt |- t : At
  :: :: target_typing :: 'TTyping_'
  {{ com Target typing }}
  by

  uniq Gt
  |- Gt
  --------------------------- :: Base
  Gt |- b : Base

  uniq Gt
  |- Gt
  x : At in Gt
  --------------------------- :: Var
  Gt |- x : At

  Gt, x:At |- t : Bt
  --------------------------- :: Abs
  Gt |- \x. t : At->Bt

  Gt, x:Bt |- t : At
  At ~= Bt
  --------------------------- :: Fix
  Gt |- fix x. t : At

  Gt |- t1 : At->Bt
  Gt |- t2 : At'
  At ~= At'
  --------------------------- :: App
  Gt |- t1 t2 : Bt

  uniq Gt
  |- Gt
  --------------------------- :: RcdNil
  Gt |- { } : Top

  recTyp Bt
  (lookup l Bt => At' and At' ~= At) or lookup l Bt =/>
  Gt |- t1 : At
  Gt |- t2 : Bt
  ----------------------------------------- :: RcdCons
  Gt |- (l => t1 , t2) :  (l : At, Bt)

  Gt |- t : At
  lookup l At => Bt
  ----------------------------------------- :: RcdProj
  Gt |- t.l : Bt

  recTyp At
  recTyp Bt
  Gt |- t1 : At
  Gt |- t2 : Bt
  At & Bt => Ct
  ---------------------------- :: RcdMerge
  Gt |- t1 ; t2 : Ct

  % Gt |- t : At
  % Gt |- t : Bt
  % ---------------------------- :: Inter
  % Gt |- t : At & Bt
  
  
% defns
% ConvertSource2Target :: '' ::=

%   defn
%   A => At :: :: styp2ttyp :: ST_
%   {{ com Try convert source types to target types }}
%   by


%   ----------------------- :: Top
%   Top => Top


%   ----------------------- :: Bot
%   Bot => Bot

 
%   ----------------------------------- :: Base
%   Base => { |Base| : Base }


%   A => At
%   B => Bt
%   ----------------------------------- :: Arrow
%   A->B => { ->|B| : At->Bt }


%   A => At
%   ----------------------------------- :: Rcd
%   {l : A} => { {l : |A|} : Top->At } 


%   A => At
%   B => Bt
%   At & Bt => Ct
%   ----------------------- :: And
%   A & B => Ct

% defns
% TargetSubtype :: '' ::=

%   defn
%   At <<: Bt
%   :: :: target_subtyping :: 'TSubtyping_'
%   {{ com Target subtyping (incomplete) }}
%   by

%   At <<: Ct
%   --------------------------- :: InterL
%   At & Bt <<: Ct

%   Bt <<: Ct
%   --------------------------- :: InterR
%   At & Bt <<: Ct


% defns
% TargetFlexTyping :: '' ::=

%   defn
%   Gt |- t <= At
%   :: :: target_flex_typing :: 'TFTyping_'
%   {{ com Target flex typing }}
%   by

%   Gt |- t : At
%   --------------------------- :: Orig
%   Gt |- t <= At

%   Gt |- t : At
%   --------------------------- :: Top
%   Gt |- t <= Top
  
%   Gt |- t : At
%   lookup l At => Bt
%   % Bt <<: Ct
%   --------------------------- :: Part
%   Gt |- t <= { l : Bt }

%   Gt |- t <= { l : At }
%   Gt |- t <= Bt
%   --------------------------- :: Cons
%   Gt |- t <= l : At , Bt

%   Gt |- t <= At
%   At ~= Bt
%   --------------------------- :: Sim
%   Gt |- t <= Bt

%   % Gt |- t <= { l : At }
%   % --------------------------- :: Proj
%   % Gt |- t.l <= At
